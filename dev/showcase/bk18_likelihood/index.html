<!DOCTYPE html>
<html lang="en"><head><meta charset="UTF-8"/><meta name="viewport" content="width=device-width, initial-scale=1.0"/><title>CosmoMC Weighted Chains (BK18 baseline likelihood analysis) · Kernel Density Estimation</title><meta name="title" content="CosmoMC Weighted Chains (BK18 baseline likelihood analysis) · Kernel Density Estimation"/><meta property="og:title" content="CosmoMC Weighted Chains (BK18 baseline likelihood analysis) · Kernel Density Estimation"/><meta property="twitter:title" content="CosmoMC Weighted Chains (BK18 baseline likelihood analysis) · Kernel Density Estimation"/><meta name="description" content="Documentation for Kernel Density Estimation."/><meta property="og:description" content="Documentation for Kernel Density Estimation."/><meta property="twitter:description" content="Documentation for Kernel Density Estimation."/><script data-outdated-warner src="../../assets/warner.js"></script><link href="https://cdnjs.cloudflare.com/ajax/libs/lato-font/3.0.0/css/lato-font.min.css" rel="stylesheet" type="text/css"/><link href="https://cdnjs.cloudflare.com/ajax/libs/juliamono/0.050/juliamono.min.css" rel="stylesheet" type="text/css"/><link href="https://cdnjs.cloudflare.com/ajax/libs/font-awesome/6.4.2/css/fontawesome.min.css" rel="stylesheet" type="text/css"/><link href="https://cdnjs.cloudflare.com/ajax/libs/font-awesome/6.4.2/css/solid.min.css" rel="stylesheet" type="text/css"/><link href="https://cdnjs.cloudflare.com/ajax/libs/font-awesome/6.4.2/css/brands.min.css" rel="stylesheet" type="text/css"/><link href="https://cdnjs.cloudflare.com/ajax/libs/KaTeX/0.16.8/katex.min.css" rel="stylesheet" type="text/css"/><script>documenterBaseURL="../.."</script><script src="https://cdnjs.cloudflare.com/ajax/libs/require.js/2.3.6/require.min.js" data-main="../../assets/documenter.js"></script><script src="../../search_index.js"></script><script src="../../siteinfo.js"></script><script src="../../../versions.js"></script><link class="docs-theme-link" rel="stylesheet" type="text/css" href="../../assets/themes/catppuccin-mocha.css" data-theme-name="catppuccin-mocha"/><link class="docs-theme-link" rel="stylesheet" type="text/css" href="../../assets/themes/catppuccin-macchiato.css" data-theme-name="catppuccin-macchiato"/><link class="docs-theme-link" rel="stylesheet" type="text/css" href="../../assets/themes/catppuccin-frappe.css" data-theme-name="catppuccin-frappe"/><link class="docs-theme-link" rel="stylesheet" type="text/css" href="../../assets/themes/catppuccin-latte.css" data-theme-name="catppuccin-latte"/><link class="docs-theme-link" rel="stylesheet" type="text/css" href="../../assets/themes/documenter-dark.css" data-theme-name="documenter-dark" data-theme-primary-dark/><link class="docs-theme-link" rel="stylesheet" type="text/css" href="../../assets/themes/documenter-light.css" data-theme-name="documenter-light" data-theme-primary/><script src="../../assets/themeswap.js"></script><link href="../../assets/citations.css" rel="stylesheet" type="text/css"/></head><body><div id="documenter"><nav class="docs-sidebar"><div class="docs-package-name"><span class="docs-autofit"><a href="../../">Kernel Density Estimation</a></span></div><button class="docs-search-query input is-rounded is-small is-clickable my-2 mx-auto py-1 px-2" id="documenter-search-query">Search docs (Ctrl + /)</button><ul class="docs-menu"><li><a class="tocitem" href="../../">Kernel Density Estimation</a></li><li><a class="tocitem" href="../../userguide/">User Guide</a></li><li><a class="tocitem" href="../../extensions/">Package Extensions</a></li><li><a class="tocitem" href="../../explain/">Explanation</a></li><li><a class="tocitem" href="../">Showcase</a></li><li><a class="tocitem" href="../../api/">API</a></li><li><a class="tocitem" href="../../releasenotes/">Release Notes</a></li><li><a class="tocitem" href="../../references/">References</a></li></ul><div class="docs-version-selector field has-addons"><div class="control"><span class="docs-label button is-static is-size-7">Version</span></div><div class="docs-selector control is-expanded"><div class="select is-fullwidth is-size-7"><select id="documenter-version-selector"></select></div></div></div></nav><div class="docs-main"><header class="docs-navbar"><a class="docs-sidebar-button docs-navbar-link fa-solid fa-bars is-hidden-desktop" id="documenter-sidebar-button" href="#"></a><nav class="breadcrumb"><ul class="is-hidden-mobile"><li class="is-active"><a href>CosmoMC Weighted Chains (BK18 baseline likelihood analysis)</a></li></ul><ul class="is-hidden-tablet"><li class="is-active"><a href>CosmoMC Weighted Chains (BK18 baseline likelihood analysis)</a></li></ul></nav><div class="docs-right"><a class="docs-settings-button docs-navbar-link fa-solid fa-gear" id="documenter-settings-button" href="#" title="Settings"></a><a class="docs-article-toggle-button fa-solid fa-chevron-up" id="documenter-article-toggle-button" href="javascript:;" title="Collapse all docstrings"></a></div></header><article class="content" id="documenter-page"><h1 id="CosmoMC-Weighted-Chains-(BK18-baseline-likelihood-analysis)"><a class="docs-heading-anchor" href="#CosmoMC-Weighted-Chains-(BK18-baseline-likelihood-analysis)">CosmoMC Weighted Chains (BK18 baseline likelihood analysis)</a><a id="CosmoMC-Weighted-Chains-(BK18-baseline-likelihood-analysis)-1"></a><a class="docs-heading-anchor-permalink" href="#CosmoMC-Weighted-Chains-(BK18-baseline-likelihood-analysis)" title="Permalink"></a></h1><p>The figure below is to be compared to <a href="http://bicepkeck.org/figures/bk-xiii.html">Figure 4 from BICEP/Keck paper XIII</a>.</p><p>After loading the appropriate columns from the set of MCMC chains and thinning, performing the kernel density estimation is as simple as:</p><pre><code class="language-julia hljs">K_r  = kde(chain_r;  weights = chain_weight, lo =  0.0,            boundary = :closedleft)
K_Ad = kde(chain_Ad; weights = chain_weight, lo =  0.0,            boundary = :closedleft)
K_As = kde(chain_As; weights = chain_weight, lo =  0.0,            boundary = :closedleft)
K_βd = kde(chain_βd; weights = chain_weight, lo =  0.8, hi =  2.4)
K_βs = kde(chain_βs; weights = chain_weight, lo = -4.5, hi = -2.0)
K_αd = kde(chain_αd; weights = chain_weight, lo = -1.0, hi =  0.0, boundary = :closed)
K_αs = kde(chain_αs; weights = chain_weight, lo = -1.0, hi =  0.0, boundary = :closed)
K_ε  = kde(chain_ε;  weights = chain_weight, lo = -1.0, hi =  1.0, boundary = :closed)</code></pre><p><img src="index.svg" alt/></p><p>Note that because this package does not support constructing 2D density estimates, the 68% and 95% confidence &quot;ellipses&quot; in the lower-left triangle of the plot has been replaced with simple 2D histograms. The hex bin sizes have been chosen using 2× the automatically-determined bandwidths of the corresponding 1D curves.</p><details class="admonition is-details"><summary class="admonition-header">Source Code</summary><div class="admonition-body"><p><a href="index.jl"><img src="https://img.shields.io/badge/code-blue?style=for-the-badge&amp;logo=julia&amp;logoColor=white" alt="download code"/></a> <a href="Project.toml"><img src="https://img.shields.io/badge/Project.toml-green?style=for-the-badge&amp;logo=toml&amp;logoColor=white" alt="download Project.toml"/></a> <a href="Manifest.toml"><img src="https://img.shields.io/badge/Manifest.toml-purple?style=for-the-badge&amp;logo=toml&amp;logoColor=white" alt="download Manifest.toml"/></a></p><pre><code class="language-julia hljs">using CSV
import KernelDensityEstimation as KDE
using .KDE


# !!!!!!!!!!
# !! NOTE !!
# !!!!!!!!!!
#
# These files must be downloaded manually and the correct folder extracted from
# http://bicepkeck.org/BK18_datarelease/chains.tgz
#
# Be warned that this file is approximately 63GiB, though we use only the smaller of the
# two datasets which expands to about 4.7GiB.

srcdir = joinpath(@__DIR__, &quot;BK18_04_BK18lf_freebdust&quot;)
filebase = joinpath(srcdir, &quot;BK18_BK18lf_freebdust&quot;)

# interpret the parameters file; first column are column names of the chain file, and
# the second column gives corresponding LaTeX pretty names
paramnames = CSV.read(filebase * &quot;.paramnames&quot;, CSV.Tables.matrix; delim = &#39;\t&#39;, header = false)

# load the chains
# keep only the MCMC parameters, not all of the derived parameters (adding the two
#   implicit columns for weights and neg-loglike)
nkeep = 2 + findlast(!endswith(&quot;*&quot;), paramnames[:, 1])
chains = mapreduce(vcat, 1:16) do num
    CSV.read(filebase * &quot;_$num.txt&quot;, CSV.Tables.matrix;
            header = false, delim=&#39; &#39;, ignorerepeated = true,
            select = 1:nkeep)
end

# use the parameter names file to find the correct column in the chains matrix
paramidx(name) = findfirst(==(name), @view paramnames[:, 1])
thinning = 50  # empirically chosen based on examining plot,
               #   lines(autocor(params[:, paramidx(&quot;r&quot;)]))

# get weights and the parameters portion of the matrix
chain_weight = @view chains[1:thinning:end, 1];
params = @view chains[1:thinning:end, 3:end];
# then extract vectors for each of the parameters we&#39;ll plot
chain_r = @view params[:, paramidx(&quot;r&quot;)];
chain_Ad = @view params[:, paramidx(&quot;BBdust&quot;)];
chain_As = @view params[:, paramidx(&quot;BBsync&quot;)];
chain_βd = @view params[:, paramidx(&quot;BBbetadust&quot;)];
chain_βs = @view params[:, paramidx(&quot;BBbetasync&quot;)];
chain_αd = @view params[:, paramidx(&quot;BBalphadust&quot;)];
chain_αs = @view params[:, paramidx(&quot;BBalphasync&quot;)];
chain_ε = @view params[:, paramidx(&quot;BBdustsynccorr&quot;)];

# run kernel density estimation on each chain
# N.B. the parameter limits in the `filebase * &quot;.ranges&quot;` was examined to determine lo/hi
K_r  = kde(chain_r;  weights = chain_weight, lo =  0.0,            boundary = :closedleft)
K_Ad = kde(chain_Ad; weights = chain_weight, lo =  0.0,            boundary = :closedleft)
K_As = kde(chain_As; weights = chain_weight, lo =  0.0,            boundary = :closedleft)
K_βd = kde(chain_βd; weights = chain_weight, lo =  0.8, hi =  2.4)
K_βs = kde(chain_βs; weights = chain_weight, lo = -4.5, hi = -2.0)
K_αd = kde(chain_αd; weights = chain_weight, lo = -1.0, hi =  0.0, boundary = :closed)
K_αs = kde(chain_αs; weights = chain_weight, lo = -1.0, hi =  0.0, boundary = :closed)
K_ε  = kde(chain_ε;  weights = chain_weight, lo = -1.0, hi =  1.0, boundary = :closed)


# !!!!!!!!!!
# !! NOTE !!
# !!!!!!!!!!
#
# Below is just plotting. Nothing interesting about KDEs...

using CairoMakie
Base.isinteractive() &amp;&amp; using GLMakie

update_theme!(
    Axis = (
        xgridvisible = false,
        ygridvisible = false,
        xtickalign = 1,
        ytickalign = 1,
    ),
    fontsize = 14,
    theme_latexfonts()
)

fig = Figure(size = (800, 800))

fig.layout.alignmode = Outside(8)  # shrink exterior padding

# use two separate sub-grids
# 1. Large Panel grid for the primary parameters (lower triangle of 1D and 2D likelihoods)
fig.layout[1:3, 1:3] = large_grid = GridLayout()
# 2. Small Panel grid to show secondary &quot;nuisance&quot; parameters, reusing just the extra
#    space in the upper-right of the large grid
fig.layout[1:2, 2:3] = small_grid = GridLayout(alignmode = Outside(18, 0, 18, 0))

# use square panels in the top-level fig layout so that the large panel axes can be
# brought into direct contact with one another (once appropriate decorations are hidden)
for ii in 1:3
    rowsize!(fig.layout, ii, Aspect(1, 1))
end

kws_like = (; linewidth = 2, color = :black)
kws_prior = (; linewidth = 1, linestyle = :dash, color = :blue)
dust_label = L&quot;$A_\mathrm{dust}$ @ $\ell=80$ &amp; 353GHz [$\mu K^2$]&quot;
sync_label = L&quot;$A_\mathrm{sync}$ @ $\ell=80$ &amp; 23GHz [$\mu K^2$]&quot;

peaknorm(K::KDE.UnivariateKDE) = KDE.UnivariateKDE(K.x, K.f ./ maximum(K.f))

#############
# large grid

# main diagonal of 1D marginalized posteriors

ax_r = let ax = Axis(large_grid[1, 1], aspect = 1)
    lines!(ax, peaknorm(K_r); kws_like...)
    hidexdecorations!(ax, ticks = false)
    ax.ylabel = L&quot;L / L_\mathrm{peak}&quot;
    xlims!(ax, 0, 0.18)
    ylims!(ax, 0, nothing)
    ax.xticks = 0:0.04:0.16
    ax.yticks = 0:0.2:1
    ax
end

ax_Ad = let ax = Axis(large_grid[2, 2])
    lines!(ax, peaknorm(K_Ad); kws_like...)
    hidexdecorations!(ax, ticks = false)
    hideydecorations!(ax, ticks = false)
    xlims!(ax, 0, 11)
    ylims!(ax, 0, nothing)
    ax.xticks = 0:2:10
    ax.yticks = 0:0.2:1
    ax
end
ax_As = let ax = Axis(large_grid[3, 3])
    lines!(ax, peaknorm(K_As); kws_like...)
    hideydecorations!(ax, ticks = false)
    xlims!(ax, 0, 8)
    ylims!(ax, 0, nothing)
    ax.xticks = 0:2:8
    ax.yticks = 0:0.2:1
    ax.xlabel = sync_label
    ax
end

# lower triangle of parameter correlations

ax_r_Ad = let ax = Axis(large_grid[2, 1])
    # KDE defaults to bwratio = 8, so factor of 16 × Δx is just 2 × bandwidth
    cellsize = 16 .* (step(K_r.x), step(K_Ad.x))
    hexbin!(ax, chain_r, chain_Ad; weights, cellsize, colormap = Reverse(:greys))
    hidexdecorations!(ax, ticks = false)
    ax.ylabel = dust_label
    xlims!(ax, 0, 0.18)
    ylims!(ax, 0, 8.5)
    ax.xticks = 0:0.04:0.16
    ax.yticks = 0:1.5:7.5
    ax
end
ax_r_As = let ax = Axis(large_grid[3, 1])
    # KDE defaults to bwratio = 8, so factor of 16 × Δx is just 2 × bandwidth
    cellsize = 16 .* (step(K_r.x), step(K_As.x))
    hexbin!(ax, chain_r, chain_As; weights, cellsize, colormap = Reverse(:greys))
    ax.xlabel = L&quot;r&quot;
    ax.ylabel = sync_label
    xlims!(ax, 0, 0.18)
    ylims!(ax, 0, 8.5)
    ax.xticks = 0:0.04:0.16
    ax.yticks = 0:1.5:7.5
    ax
end
ax_Ad_As = let ax = Axis(large_grid[3, 2])
    # KDE defaults to bwratio = 8, so factor of 16 × Δx is just 2 × bandwidth
    cellsize = 16 .* (step(K_Ad.x), step(K_As.x))
    hexbin!(ax, chain_Ad, chain_As; weights, cellsize, colormap = Reverse(:greys))
    hideydecorations!(ax, ticks = false)
    ax.xlabel = dust_label
    xlims!(ax, 0, 11)
    ylims!(ax, 0, 8.5)
    ax.xticks = 0:2:10
    ax.yticks = 0:1.5:7.5
    ax
end

# close the gaps in the grid
rowgap!(large_grid, Fixed(0.0))
colgap!(large_grid, Fixed(0.0))

#############
# small grid

ax_βd = let ax = Axis(small_grid[1, 1], aspect = 1)
    # prior
    lines!(ax, [0.8, 0.8, 2.4, 2.4], [0, 1, 1, 0]; kws_prior...)
    # posterior curve
    lines!(ax, peaknorm(K_βd); kws_like...)
    xlims!(ax, 1, 2)
    ylims!(ax, 0, nothing)
    ax.xlabel = L&quot;\beta_d&quot;
    ax.yticks = 0:1
    ax
end

ax_βs = let ax = Axis(small_grid[1, 2], aspect = 1)
    # prior
    let μ = -3.1, σ = 0.3
        lines!(ax, -4.5:0.01:-1.8, x -&gt; exp(-(x - μ)^2 / (2σ^2)); kws_prior...)
    end
    # posterior curve
    lines!(ax, peaknorm(K_βs); kws_like...)
    xlims!(ax, -4.5, -1.8)
    ylims!(ax, 0, nothing)
    ax.xlabel = L&quot;\beta_s&quot;
    ax.xticks = -4:-2
    ax.yticks = 0:1
    ax
end

ax_ε = let ax = Axis(small_grid[1, 3], aspect = 1)
    # prior
    lines!(ax, [-1, -1, 1, 1], [0, 1, 1, 0]; kws_prior...)
    # posterior curve
    lines!(ax, peaknorm(K_ε); kws_like...)
    ylims!(ax, 0, nothing)
    ax.xlabel = L&quot;\varepsilon&quot;
    ax.xticks = -1:1
    ax.yticks = 0:1
    ax
end

ax_αd = let ax = Axis(small_grid[2, 3], aspect = 1)
    # prior
    lines!(ax, [-1, -1, 0, 0], [0, 1, 1, 0]; kws_prior...)
    # posterior curve
    lines!(ax, peaknorm(K_αd); kws_like...)
    ylims!(ax, 0, nothing)
    ax.xlabel = L&quot;\alpha_d&quot;
    ax.yticks = 0:1
    ax
end

ax_αs = let ax = Axis(small_grid[3, 3], aspect = 1)
    # prior
    lines!(ax, [-1, -1, 0, 0], [0, 1, 1, 0]; kws_prior...)
    # posterior curve
    lines!(ax, peaknorm(K_αs); kws_like...)
    ylims!(ax, 0, nothing)
    ax.xlabel = L&quot;\alpha_s&quot;
    ax.yticks = 0:1
    ax
end

# adjust the small grid to also have square panels
for ii in 1:3
    colsize!(small_grid, ii, Aspect(2, 1))
end
# grow the column spacing to better match the vertical spacing (which includes labels)
colgap!(small_grid, Fixed(44))
# then keep the small grid top-right aligned w.r.t. the main figure grid, since the
# forced aspect ratios require some padding be added somewhere
small_grid.valign = :top
small_grid.halign = :right

save(&quot;index.svg&quot;, fig, backend = CairoMakie)
</code></pre></div></details></article><nav class="docs-footer"><p class="footer-message">Powered by <a href="https://github.com/JuliaDocs/Documenter.jl">Documenter.jl</a> and the <a href="https://julialang.org/">Julia Programming Language</a>.</p></nav></div><div class="modal" id="documenter-settings"><div class="modal-background"></div><div class="modal-card"><header class="modal-card-head"><p class="modal-card-title">Settings</p><button class="delete"></button></header><section class="modal-card-body"><p><label class="label">Theme</label><div class="select"><select id="documenter-themepicker"><option value="auto">Automatic (OS)</option><option value="documenter-light">documenter-light</option><option value="documenter-dark">documenter-dark</option><option value="catppuccin-latte">catppuccin-latte</option><option value="catppuccin-frappe">catppuccin-frappe</option><option value="catppuccin-macchiato">catppuccin-macchiato</option><option value="catppuccin-mocha">catppuccin-mocha</option></select></div></p><hr/><p>This document was generated with <a href="https://github.com/JuliaDocs/Documenter.jl">Documenter.jl</a> version 1.8.0 on <span class="colophon-date" title="Tuesday 7 January 2025 23:52">Tuesday 7 January 2025</span>. Using Julia version 1.11.2.</p></section><footer class="modal-card-foot"></footer></div></div></div></body></html>
